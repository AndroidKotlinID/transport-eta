apply plugin: 'com.android.application'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion build_versions.target_sdk
    defaultConfig {
        applicationId "com.joaquimley.transporteta"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode app_version.versionCode
        versionName app_version.versionName
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "com.joaquimley.transporteta.ui.test.TestRunner"

        missingDimensionStrategy 'environment', 'mock', 'prod'
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
    }

    buildTypes {
        debug {
            versionNameSuffix "-dev"
            applicationIdSuffix ".debug"
            debuggable true
            testCoverageEnabled true
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

//    productFlavors {
//        flavorDimensions "city", "environment"
//
//        lx {
//           dimension "city"
//        }
//
//        dev {
//            applicationIdSuffix ".dev"
//            dimension "environment"
//        }
//
//        mock {
//            applicationIdSuffix ".mock"
//            dimension "environment"
//        }
//    }
}

kapt {
    correctErrorTypes = true
}

androidExtensions {
    experimental = true
}

configurations.all {
    resolutionStrategy {
        force deps.kotlin.stdlib
    }
}

dependencies {
    // Modules
    implementation project(':sms')

    // Javax
    implementation deps.javax.inject
    compileOnly deps.javax.annotation
    // Rx
    implementation deps.rxjava2
    implementation deps.rx_android
    // Kotlin
    implementation deps.kotlin.rx
    implementation deps.kotlin.stdlib
    // Dagger
    implementation deps.dagger.runtime
    implementation deps.dagger.android
    implementation deps.dagger.android_support
    kapt deps.dagger.compiler
    kapt deps.dagger.android_support_compiler
    // Support
    implementation deps.support.v4
    implementation deps.support.design
    implementation deps.support.cardview
    implementation deps.support.app_compat
    implementation deps.support.constraint
    implementation deps.support.annotations
    implementation deps.support.recyclerview
    // Utils
    implementation deps.glide
    implementation deps.timber
    implementation deps.lottie
    implementation deps.kotterknife
    // ACC
    kapt deps.lifecycle.compiler
    implementation deps.lifecycle.extensions
    // Unit Testing
    kaptTest deps.dagger.compiler
    kaptAndroidTest deps.dagger.compiler
    testImplementation deps.junit
    testImplementation deps.hamcrest
    testImplementation deps.kotlin.test
    testImplementation deps.mockito.core
    testImplementation deps.arch_core.testing
    // Instrumentation testing
    androidTestImplementation deps.kotlin.mockito
    androidTestImplementation(deps.atsl.runner) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(deps.atsl.rules) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(deps.espresso.core) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(deps.espresso.intents) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(deps.espresso.contrib) {
        exclude module: 'appcompat'
        exclude module: 'appcompat-v7'
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'support-annotations'
        exclude module: 'recyclerview-v7'
        exclude module: 'design'
    }
}